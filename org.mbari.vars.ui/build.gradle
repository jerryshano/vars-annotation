plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version "0.0.8"
    id 'org.beryx.jlink' version "2.14.0"
}

dependencies {
    compile project(':org.mbari.vars.core')
//    compile project(':org.mbari.vars.javafx')
    compile project(':org.mbari.vars.services')

    implementation 'com.google.guava:guava'
    implementation 'com.google.inject:guice'
    implementation 'com.jfoenix:jfoenix'
    implementation 'com.typesafe:config'
    implementation 'io.reactivex.rxjava2:rxjava'
    implementation 'org.kordamp.ikonli:ikonli-javafx'
    implementation 'org.kordamp.ikonli:ikonli-material-pack'
    implementation 'org.mbari.vars:vars-avfoundation'
    implementation 'org.mbari.vcr4j:vcr4j-core'
    implementation 'org.mbari.vcr4j:vcr4j-jserialcomm'
    implementation 'org.mbari.vcr4j:vcr4j-sharktopoda'
    implementation 'org.mbari:mbarix4j'
    implementation 'org.slf4j:slf4j-api'

    implementation('io.reactivex.rxjava2:rxjavafx') {
        exclude group: 'org.openjfx', module: 'javafx-base'
        exclude group: 'org.openjfx', module: 'javafx-controls'
        exclude group: 'org.openjfx', module: 'javafx-graphics'
    }

    implementation('org.controlsfx:controlsfx') {
        exclude group: 'org.openjfx', module: 'javafx-base'
        exclude group: 'org.openjfx', module: 'javafx-controls'
        exclude group: 'org.openjfx', module: 'javafx-graphics'
    }

    implementation('org.mbari.m3:vars-blackmagic') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

    runtimeOnly 'ch.qos.logback:logback-classic'
    runtimeOnly 'javax.servlet:javax.servlet-api'
}

javafx {
    version = "11.0.2"
    modules = [
            'javafx.base',
            'javafx.controls',
            'javafx.fxml',
            'javafx.graphics'
    ]
}

// These runtime args are shared by several tasks
def runtimeJvmArgs = [
        '-Xms1g',
        '--add-exports', 'javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix',
        '--add-exports', 'javafx.controls/com.sun.javafx.scene.control=com.jfoenix',
        '--add-exports', 'javafx.base/com.sun.javafx.binding=com.jfoenix',
        '--add-exports', 'javafx.graphics/com.sun.javafx.stage=com.jfoenix',
        '--add-exports', 'javafx.base/com.sun.javafx.event=com.jfoenix'
]

application {
    mainClassName = "org.mbari.vars.ui/org.mbari.vars.ui.App"
    applicationDefaultJvmArgs = runtimeJvmArgs
}



/* *****************************************************************************
 Use the jlink plugin to build a dist with a JVM
 */
jlink {
    imageZip = file("$buildDir/image-zip/vars-annotation.zip")
    options = [
            '--bind-services',
            '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages',
    ]

    if(System.getenv("CI")) {
        ["win", "linux", "mac"].each { name ->
            targetPlatform(name) {
                jdkHome = System.getenv("JDK_HOME_${name.toUpperCase()}")
                addExtraModulePath(System.getenv("OPENJFX_MODS_${name.toUpperCase()}"))
            }
        }
    }

    launcher {
        name = 'vars-annotation'
        jvmArgs = runtimeJvmArgs
    }

    jpackage {
        jpackageHome = System.getenv("JPACKAGE_HOME")
    }

}